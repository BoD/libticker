description = 'libticker-plugins'

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

compileKotlin {
    kotlinOptions {
        jvmTarget = versions.build.kotlinJvmTarget
        // Ensure the kotlin module name is unique
        freeCompilerArgs += ['-module-name', "$project.group.$project.name"]
    }
}

javadoc.failOnError = false

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId project.description

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    // JUnit
    testImplementation "junit:junit:$versions.junit"

    // Rx
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rxKotlin"

    // Twitter4J
    implementation "org.twitter4j:twitter4j-core:$versions.twitter4j"

    // ForecasIO
    implementation "com.github.dvdme:ForecastIOLib:$versions.forecastIo"

    // Sunrise sunset
    implementation "ca.rmen:lib-sunrise-sunset:$versions.sunriseSunset"

    // FRC
    implementation "ca.rmen:lib-french-revolutionary-calendar:$versions.libFrc"

    // Klaxon
    implementation "com.beust:klaxon:$versions.klaxon"

    // Plugin api
    implementation project(':plugin-api')

    // Slf4j
    implementation "org.slf4j:slf4j-api:$versions.slf4j"
}

// Use "./gradlew publishToMavenLocal" to deploy the artifacts to your local maven repository
