description = 'libticker-httpconf'

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

compileKotlin {
    kotlinOptions {
        jvmTarget = versions.build.kotlinJvmTarget
        // Ensure the kotlin module name is unique
        freeCompilerArgs += ['-module-name', "$project.group.$project.name"]
    }
}

javadoc.failOnError = false

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId project.description

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    
    // Ktor
    implementation "io.ktor:ktor-server-netty:$versions.ktor"
    implementation "io.ktor:ktor-freemarker:$versions.ktor"
    
    // Slf4j
    implementation "org.slf4j:slf4j-simple:$versions.slf4j"

    // JUnit
    testImplementation "junit:junit:$versions.junit"

    // Plugin api
    implementation project(':core')
}

// Use "./gradlew publishToMavenLocal" to deploy the artifacts to your local maven repository
